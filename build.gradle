//def corePlugin = 'com.mcmoddev.lib.asm.ASMPlugin'

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'gradle'
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public'
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'jacoco'

repositories {
	mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile('org.junit.jupiter:junit-jupiter-api:5.0.2')
    testCompile('org.mockito:mockito-core:2.+')
    testCompile('org.junit.platform:junit-platform-launcher:1.0.2')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.0.2')
}

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    // configure jacoco to analyze the junitPlatformTest task
    jacoco {
        // this tool version is compatible with
        toolVersion = "0.7.6.201602180812"
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport",
            {
                sourceDirectories = files("./src/main")
                classDirectories = files("$buildDir/classes/main")
                executionData junitPlatformTestTask
            })
}

junitPlatform {
    platformVersion '1.0.2' // optional, defaults to plugin version
    //logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform') // this is the default
    // enableStandardTestTask true
    // selectors (optional)
    // filters (optional)
}


javadoc {
    failOnError = false
}

description = 'MMDLib'
def mod_file = getModFile()
def mc_version = "1.12"
def short_version = getVersion("VERSION", mod_file)
version = mc_version + "-" + short_version

if (System.getenv().BUILD_NUMBER) {
    version += '.' + System.getenv().BUILD_NUMBER
}
group = 'com.mcmoddev'
archivesBaseName = 'MMDLib'
sourceCompatibility = targetCompatibility = '1.8'

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ''
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file 'secret.json'
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = "1.12.2-14.23.2.2611"
    runDir = "run"
    mappings = "snapshot_20180223"

    if (project.hasProperty('coreplugin')) {
        clientJvmArgs += ['-Dfml.coreMods.load', project.coreplugin]
        serverJvmArgs += ['-Dfml.coreMods.load', project.coreplugin]
    }
}

repositories {
    maven { // Mantle, TCon, JEI
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven { // CCL, CCC, NEI
        name 'Chickenbones Repo'
        url 'http://chickenbones.net/maven/'
    }
    maven { // The One Probe
        name 'tterrag'
        url 'http://maven.tterrag.com/'
    }
    maven {
        name 'MMD'
        url 'https://maven.mcmoddev.com/'
    }
    maven { // MCMultipart
        name 'amadornes'
        url 'http://maven.amadornes.com/'
    }
    maven { // CraftTweaker, ZenScript
        name 'jared maven'
        url 'http://blamejared.com/maven'
    }
    maven { // IC2
        name "industrialcraft"
        url "http://maven.ic2.player.to"
    }
    maven { // WAILA/HWYLA
    	name 'tehnut'
    	url 'http://tehnut.info/maven'
    }
    maven { // CoFH
    	name 'Covers Maven'
    	url 'http://maven.covers1624.net'
    }
	maven {
        name = 'CurseForge'
        url = 'https://minecraft.curseforge.com/api/maven/'
    }
	maven {
        name 'opencomputers'
        url 'http://maven.cil.li/'
    }
    maven { // Mekanism, TAIGA
        name 'jitpack'
        url 'https://jitpack.io'
    }
    maven { // Buildcraft
        name 'buildcraft'
	url 'https://www.mod-buildcraft.com/maven'
    }
}

dependencies {
    deobfProvided ("com.github.aidancbrady:Mekanism:${mekanism_version}") {
        exclude group: 'MCMultiPart'
        exclude group: 'mezz.jei'
    }

    deobfProvided ("slimeknights:TConstruct:${mc_version}-${tconstruct_version}") {
        exclude group: 'mezz.jei'
    }
    deobfProvided ("slimeknights.mantle:Mantle:${mc_version}-${mantle_version}") {
        exclude group: 'mezz.jei'
    }
	
//  We do this to make NEI happy as there is no deobf or dev JEI jar for the deobf instance
    deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}"

    deobfProvided "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal"
    deobfProvided ("codechicken:NotEnoughItems:${mc_version}-${nei_version}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'codechicken'
    }

    deobfProvided ("net.industrial-craft:industrialcraft-2:${ic2_version}:api") {
        exclude group: 'mezz.jei'
    }

    deobfProvided ("MineTweaker3:MineTweaker3-API:${minetweaker_version}") {
        exclude group: 'mezz.jei'
    }

    deobfProvided ("mcp.mobius.waila:Hwyla:${hwyla_version}") {
        exclude group: 'mezz.jei'
    }
    
    // CoFHCore for TE integration
    deobfProvided ("cofh:CoFHCore:${mc_version}-${cofhcore_version}:deobf") {
    	exclude group: 'mezz.jei'
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': short_version, 'mcversion': project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
//        attributes 'FMLCorePluginContainsFMLMod': 'true'
//        attributes 'FMLCorePlugin': corePlugin
    }
}

task deobfJar(type: Jar) {
    classifier = 'deobf'
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact deobfJar {
                classifier 'deobf'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    apiKey = secret.curseforgeAPIKey
    project {
        id = '261744'
        changelog = file('CHANGELOG.txt')
        releaseType = 'beta'
        addGameVersion(project.mc_version)
        def projectName = 'MMDLib'
        def displayVersion = getVersion('VERSION', mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += '.' + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "$project.description $displayVersion"
            relations {
                requiredLibrary 'mmd-orespawn'
                optionalLibrary 'additional-loot-tables'
            }
        }
        addArtifact(sourceJar) {
            displayName = "$project.description $displayVersion Sources"
        }
        addArtifact(deobfJar) {
            displayName = "$project.description $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "$project.description $displayVersion Javadoc"
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', secret.sonarHost
        property 'sonar.organization', secret.sonarOrganization
        property 'sonar.login', secret.sonarToken
        property 'sonar.projectName', project.archivesBaseName
        property 'sonar.projectKey', "$project.group:$project.archivesBaseName"
    }
}

String getModFile() {
    String path = ''
    FileTree tree = fileTree(dir: 'src/main/java')
    tree.include '**/*.java'
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim()
                if (s.startsWith('@Mod(')) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path
}

String getVersion(String type, String mod_file) {
    String major = '0'
    String revision = '0'
    String patch = '0'
    String prefix = "public static final String $type = \""
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim()
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2)
            String[] pts = s.split("\\.")

            major = pts[0]
            revision = pts[1]
            patch = pts[2]
        }
    }
    return "$major.$revision.$patch"
}
